## Workflow to run GangSTR (https://github.com/gymreklab/GangSTR), a tool
## for computing genome-wide profile of short tandem repeats (STR) from
## short reads.

version 1.0

import "Structs.wdl"

workflow GangSTR {

  input {
    File bam_or_cram
    File? bam_or_cram_index
    File reference_fasta
    File? reference_fasta_index
    File target_tr_loci_regions_bed
    String? output_prefix
    String str_docker
    RuntimeAttr? runtime_attr
  }

  parameter_meta {
    bam_or_cram: "Set the path to a sorted and indexed bam or cram file generated by an indel-sensitive aligner to be used as input for GangSTR."
    bam_or_cram_index: "[Optional] Set the path to the index file of the `bam_or_cram` input."
    reference_fasta: "Sets the path to the reference in fasta format."
    reference_fasta_index: "[Optional] Sets the path to the index of reference file."
    target_tr_loci_regions_bed: "Reference set of regions to genotype represented in bed-like format; see GangSTR documentation for the file structure at: https://github.com/gymreklab/GangSTR#tr-regions---regions"
    output_prefix: "[Optional] Set an string to be used as a prefix to the output files. Defaults to the bam_or_cram filename."
    str_docker: "Sets the STR docker image."
    runtime_attr: "[Optional] Override the default runtime attributes for the GangSTR workflow."
  }

  Boolean is_bam =
    basename(bam_or_cram, ".bam") + ".bam" == basename(bam_or_cram)

  File bam_or_cram_index_ =
    if defined(bam_or_cram_index) then
      select_first([bam_or_cram_index])
    else
      bam_or_cram + if is_bam then ".bai" else ".crai"

  File reference_fasta_index_ = select_first([
    reference_fasta_index, reference_fasta + ".fai"])

  String output_prefix_ =
    if defined(output_prefix) then
      select_first([output_prefix])
    else
      if is_bam then
        basename(bam_or_cram, ".bam")
      else
        basename(bam_or_cram, ".cram")

  call CallGangSTR {
    input:
      bam_or_cram = bam_or_cram,
      bam_or_cram_index = bam_or_cram_index_,
      reference_fasta = reference_fasta,
      reference_fasta_index = reference_fasta_index_,
      target_tr_loci_regions_bed = target_tr_loci_regions_bed,
      output_prefix = output_prefix_,
      str_docker = str_docker,
      runtime_attr_override = runtime_attr
  }

  output {
    File output_vcf = CallGangSTR.output_vcf
    File sample_stats = CallGangSTR.sample_stats
    File insdata = CallGangSTR.insdata
  }
}

task CallGangSTR {
  input {
    File bam_or_cram
    File bam_or_cram_index
    File reference_fasta
    File reference_fasta_index
    File target_tr_loci_regions_bed
    String output_prefix
    String str_docker
    RuntimeAttr? runtime_attr_override
  }

  output {
    File output_vcf = "${output_prefix}.vcf"
    File sample_stats = "${output_prefix}.samplestats.tab"
    File insdata = "${output_prefix}.insdata.tab"
  }

  command <<<
    set -euxo pipefail

    GangSTR \
      --bam ~{bam_or_cram} \
      --ref ~{reference_fasta} \
      --regions ~{target_tr_loci_regions_bed} \
      --out ~{output_prefix}
  >>>

  RuntimeAttr default_attr = object {
    cpu_cores: 1,
    mem_gb: 4,
    boot_disk_gb: 10,
    preemptible_tries: 3,
    max_retries: 1,
    disk_gb: 10 + ceil(size([
      bam_or_cram,
      reference_fasta,
      reference_fasta_index], "GiB"))
  }
  RuntimeAttr runtime_attr = select_first([
    runtime_attr_override,
    default_attr])

  runtime {
    docker: str_docker
    cpu: runtime_attr.cpu_cores
    memory: select_first([runtime_attr.mem_gb, default_attr.mem_gb]) + " GiB"
    disks: "local-disk " + select_first([runtime_attr.disk_gb, default_attr.disk_gb]) + " HDD"
    bootDiskSizeGb: runtime_attr.boot_disk_gb
    preemptible: runtime_attr.preemptible_tries
    maxRetries: runtime_attr.max_retries
  }
}
